/*
 * MIT License
 *
 * Copyright (c) 2017, Ilia Rogozhin (ilia.rogozhin@gmail.com)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

group 'com.github.smallcreep'
version '0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'wrapper'
apply plugin: 'project-report'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'build-dashboard'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

wrapper {
    gradleVersion = '3.5'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

project.ext {
    junitVersion = "4.12"
    lombokVersion = "1.16.10"
    jcabiHttpVersion = "1.17"
    jcabiAspectsVersion = "0.22.6"
    javaxJsonVersion = "1.1"
    jcabiManifestsVersion = "1.1"
    jsr311ApiVersion = "1.1.1"
    hamcrestLibraryVersion = "1.3"
    grizzlyServletWwebserverVersion = "2.0.0-M3"
    httpclientVersion = "4.5.3"
    jcabiLogVersion = "0.17.2"
    log4jVersion = "2.8.2"
    jerseyClientVersion = "1.19.3"
    jcabiXmlVersion = "0.18.1"
    cactoosVersion = "0.2"
    jcabiImmutableVersion = "1.4"
    equalsverifierVersion = "2.3"
}

dependencies {
    compile "org.projectlombok:lombok:$lombokVersion"
    compile "com.jcabi:jcabi-http:$jcabiHttpVersion"
    compile "com.jcabi:jcabi-aspects:$jcabiAspectsVersion"
    compile "javax.json:javax.json-api:$javaxJsonVersion"
    compile "com.jcabi:jcabi-manifests:$jcabiManifestsVersion"
    compile "javax.ws.rs:jsr311-api:$jsr311ApiVersion"
    compile "org.hamcrest:hamcrest-library:$hamcrestLibraryVersion"
    compile "com.jcabi:jcabi-log:$jcabiLogVersion"
    compile "com.jcabi:jcabi-xml:$jcabiXmlVersion"
    compile "org.cactoos:cactoos:$cactoosVersion"
    compile "com.jcabi:jcabi-immutable:$jcabiImmutableVersion"
    compile "nl.jqno.equalsverifier:equalsverifier:$equalsverifierVersion"

    runtime "org.glassfish:javax.json:$javaxJsonVersion"
    runtime "com.sun.jersey:jersey-client:$jerseyClientVersion"

    testCompile "junit:junit:$junitVersion"

    testRuntime "com.sun.grizzly:grizzly-servlet-webserver:$grizzlyServletWwebserverVersion"
    testRuntime "org.apache.httpcomponents:httpclient:$httpclientVersion"
    testRuntimeClasspath "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    testRuntimeClasspath "org.apache.logging.log4j:log4j-core:$log4jVersion"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

checkstyle.configFile = file(project.rootDir.absolutePath + '/config/checkstyle/checks.xml')

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    systemProperty "failsafe.gitlab.key", System.getProperty("failsafe.gitlab.key")
    reports {
        html.enabled true
        junitXml.enabled true
    }
}

tasks.buildDashboard.dependsOn(build, htmlDependencyReport, jacocoTestReport, jacocoTestCoverageVerification)
